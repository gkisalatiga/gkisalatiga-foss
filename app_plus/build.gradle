//file:noinspection SpellCheckingInspection
plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"

    // Add the Google services Gradle plugin
    id "com.google.gms.google-services"
}

android {
    namespace "org.gkisalatiga.plus"
    compileSdk 35

    // For generating native debug symbols.
    // SOURCE: https://stackoverflow.com/a/63436935
    // Upgraded from v25.1.8937393 to v28.2.13676358 on 2025-09-09 as to comply with Android's
    // new 16K RAM paging size policy.
    ndkVersion "28.2.13676358"

    defaultConfig {
        applicationId "org.gkisalatiga.plus"
        minSdk 21
        targetSdk 35
        versionCode 45
        versionName "0.8.0"

        // testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            // Enables on-the-fly edit of drawable vectors.
            // SOURCE: https://stackoverflow.com/a/38418049
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type. Make sure to use a build
            // variant with `debuggable false`.
            // SOURCE: https://developer.android.com/build/shrink-code#groovy
            minifyEnabled true

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            // SOURCE: https://developer.android.com/build/shrink-code#groovy
            shrinkResources true

            // Enable native code inspection so that we can debug and trace errors
            // after R8 shrinkage on production.
            // SOURCE: https://developer.android.com/build/shrink-code#native-crash-support
            ndk {
                debugSymbolLevel "SYMBOL_TABLE"
            }

            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }

        // Such an ache when you have to face errors due to mismatching package ID
        // with Google Services and App Provider during debug!
        // Commented out on 2025-08-19.
        /*
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '.debug'
        }
        */
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "1.8"

        // SOURCE: https://stackoverflow.com/q/65545018
        freeCompilerArgs += [
                "-P",
                "plugin:androidx.compose.compiler.plugins.kotlin:suppressKotlinVersionCompatibilityCheck=true"
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        // The default version.
        // kotlinCompilerExtensionVersion '1.3.2'

        // Newer version that can be used.
        // kotlinCompilerExtensionVersion = '1.5.14'

        // You need a compatible Kotlin compiler version to use Jetpack Compose.
        // The table of compatibility list is listed below.
        // SOURCE: https://developer.android.com/jetpack/androidx/releases/compose-kotlin
        // kotlinCompilerExtensionVersion = '1.3.0'
        // kotlinCompilerExtensionVersion = '1.4.6'
        kotlinCompilerExtensionVersion = "1.5.13"
    }
    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

dependencies {

    //noinspection GradleDependency
    implementation "androidx.core:core-ktx:1.8.0"
    //noinspection GradleDependency
    implementation "androidx.appcompat:appcompat:1.7.0"
    //noinspection GradleDependency
    implementation "com.google.android.material:material:1.12.0"
    //noinspection GradleDependency
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"
    //noinspection GradleDependency
    implementation "androidx.activity:activity-compose:1.5.1"
    //noinspection GradleDependency
    implementation platform("androidx.compose:compose-bom:2022.10.00")
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-graphics"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.material3:material3"
    testImplementation "junit:junit:4.13.2"
    //noinspection GradleDependency
    androidTestImplementation "androidx.test.ext:junit:1.2.1"
    //noinspection GradleDependency
    androidTestImplementation "androidx.test.espresso:espresso-core:3.6.1"
    //noinspection GradleDependency
    androidTestImplementation platform("androidx.compose:compose-bom:2022.10.00")
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"

    // Reflection, used for debugging.
    // SOURCE: https://stackoverflow.com/a/53783441
    // implementation("org.jetbrains.kotlin:kotlin-reflect")

    // Enable screen navigation in Jetpack Compose
    // SOURCE: https://medium.com/@KaushalVasava/navigation-in-jetpack-compose-full-guide-beginner-to-advanced-950c1133740
    //noinspection GradleDependency
    implementation("androidx.navigation:navigation-compose:2.7.7")

    // Enable ModalDrawerSheet functionality
    // SOURCE: https://www.composables.com/material3/modalbottomsheet
    //noinspection GradleDependency
    implementation("androidx.compose.material3:material3:1.3.0-beta04")

    // Enable loading async images from the internet
    // SOURCE: https://github.com/coil-kt/coil#jetpack-compose
    // SOURCE: https://developer.android.com/develop/ui/compose/graphics/images/loading
    // SOURCE: https://coil-kt.github.io/coil/compose/
    implementation("io.coil-kt:coil-compose:2.7.0")

    // Enable the customizable YouTube embeddable player.
    // SOURCE: https://dev.to/mozeago/jetpack-compose-loadinghow-to-load-a-youtube-video-or-youtube-livestream-channel-to-your-android-application-4ffc
    // SOURCE: https://github.com/PierfrancescoSoffritti/android-youtube-player
    implementation("com.pierfrancescosoffritti.androidyoutubeplayer:core:12.1.0")
    implementation("com.pierfrancescosoffritti.androidyoutubeplayer:custom-ui:12.1.0")

    // Add more material icons.
    // SOURCE: https://stackoverflow.com/a/65665564
    implementation("androidx.compose.material:material-icons-extended")

    // Enable markdown rendering with customizable fonts.
    // SOURCE: https://www.perplexity.ai/search/best-markdown-renderer-for-jet-e2J8laOcTPuvhZyepYE10Q
    // SOURCE: https://github.com/jeziellago/compose-markdown
    implementation("com.github.jeziellago:compose-markdown:0.5.3")

    // To allow WorkManager to start background services and works, even after device restart.
    // SOURCE: https://medium.com/@denniskirimi.dk/android-workmanager-37e08c070439
    //noinspection GradleDependency
    implementation("androidx.work:work-runtime-ktx:2.9.1")

    // Better, more advanced VerticalPager and HorizontalPager zoomable modifier.
    implementation("net.engawapg.lib:zoomable:1.6.2")

    // Splash screen API for Android 12+ devices.
    // SOURCE: https://developer.android.com/develop/ui/views/launch/splash-screen/migrate
    implementation("androidx.core:core-splashscreen:1.0.1")

    // Firebase support.
    // When using the BoM, don't specify versions in Firebase dependencies
    // SOURCE: https://firebase.google.com/docs/android/setup?authuser=0
    //noinspection GradleDependency
    implementation platform("com.google.firebase:firebase-bom:33.1.0")
    implementation "com.google.firebase:firebase-analytics"

}